load("@io_bazel_rules_go//go:def.bzl", "go_library", "go_test")
load("//golangci-lint:def.bzl", "ANALYZERS", "golangci_lint_analyzers")
load("//private:deps_test.bzl", "prune_ext_deps")
load("//private:gen_deps.bzl", "generate_deps")

exports_files([
    "def.bzl",
    "deps.bzl",
])

[
    go_library(
        name = analyzer,
        srcs = ["analyzer.go"],
        importpath = "github.com/sluongng/nogo-analyzer/golangci-lint/" + analyzer,
        visibility = ["//visibility:public"],
        x_defs = {"name": analyzer},
        deps = ["//golangci-lint/util"],
    )
    for analyzer in ANALYZERS
]

[
    go_test(
        name = analyzer + "_test",
        size = "small",
        srcs = ["analyzer_test.go"],
        embed = [analyzer],
        # Only compatible with platforms which have sandbox execution support.
        # For more information see https://github.com/bazelbuild/rules_go/issues/3144
        target_compatible_with = select({
            "@platforms//os:osx": [],
            "@platforms//os:linux": [],
            "//conditions:default": ["@platforms//:incompatible"],
        }),
    )
    for analyzer in ANALYZERS
]

generate_deps(
    name = "golangci_lint",
    mod_file = ":go.mod",
    sum_file = ":go.sum",
)

prune_ext_deps(
    name = "prune_deps",
    deps_file = "//golangci-lint:deps.bzl",
    targets = golangci_lint_analyzers(ANALYZERS, "//golangci-lint"),
)

# gazelle:lang test_filegroup

filegroup(
    name = "all_files",
    testonly = True,
    srcs = [
        "BUILD.bazel",
        "README.md",
        "analyzer.go",
        "analyzer_test.go",
        "def.bzl",
        "deps.bzl",
        "go.mod",
        "go.sum",
        "//golangci-lint/cmd:all_files",
        "//golangci-lint/constructor:all_files",
        "//golangci-lint/patch:all_files",
        "//golangci-lint/tests:all_files",
        "//golangci-lint/util:all_files",
    ],
    visibility = ["//visibility:public"],
)
